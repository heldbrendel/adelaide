buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-tomcat-plugin:2.4.2'
    }
}

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'application'

apply plugin: 'maven'

apply plugin: 'idea'
apply plugin: 'eclipse'

apply plugin: 'com.bmuschko.tomcat'

group = 'com.gitblit'
version = '2.0.0-SNAPSHOT'

description = """Gitblit"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "http://repo1.maven.org/maven2" }
    maven { url "http://repo.eclipse.org/content/groups/snapshots" }
    maven { url "http://repo.eclipse.org/content/groups/releases" }
    maven { url "http://gitblit.github.io/gitblit-maven" }
}

compileJava.options.encoding = 'UTF-8'

sourceSets {
    main {
        java { srcDirs = ['src/main/java', 'src/main/bugtraq', 'src/main/gen'] }
        resources { srcDirs = ['src/main/resources'] }
    }
    test {
        java { srcDirs = ['src/test/java', 'src/test/bugtraq'] }
        resources { srcDirs = ['src/test/resources'] }
    }
}

/*
jar {
    manifest {
        attributes 'Main-Class': 'com.gitblit.GitBlitServer'
    }
}
*/

mainClassName = "com.gitblit.GitBlitServer"

dependencies {
    compile 'com.google.inject:guice:4.2.0'
    compile 'com.google.inject.extensions:guice-servlet:4.2.0'
    compile 'com.google.guava:guava:24.0-jre'

    compile 'org.jetbrains:annotations:15.0'

    compile 'ch.qos.logback:logback-classic:1.2.3'
    compile 'org.slf4j:slf4j-api:1.7.12'

    compile 'com.sun.mail:javax.mail:1.6.1'
    compile 'javax.servlet:javax.servlet-api:4.0.0'

    compile 'org.eclipse.jetty.aggregate:jetty-all:9.4.8.v20171121'

    compile group: 'org.apache.wicket', name: 'wicket', version: '1.4.22'
    compile group: 'org.apache.wicket', name: 'wicket-auth-roles', version: '1.4.22'
    compile group: 'org.apache.wicket', name: 'wicket-extensions', version: '1.4.22'

    compile group: 'org.apache.lucene', name: 'lucene-core', version: '7.2.1'
    compile group: 'org.apache.lucene', name: 'lucene-analyzers-common', version: '7.2.1'
    compile group: 'org.apache.lucene', name: 'lucene-highlighter', version: '7.2.1'
    compile group: 'org.apache.lucene', name: 'lucene-memory', version: '7.2.1'
    compile group: 'org.apache.lucene', name: 'lucene-queryparser', version: '7.2.1'

    compile group: 'org.pegdown', name: 'pegdown', version: '1.6.0'

    compile group: 'org.fusesource.wikitext', name: 'wikitext-core', version: '1.4'
    compile group: 'org.fusesource.wikitext', name: 'twiki-core', version: '1.4'
    compile group: 'org.fusesource.wikitext', name: 'textile-core', version: '1.4'
    compile group: 'org.fusesource.wikitext', name: 'tracwiki-core', version: '1.4'
    compile group: 'org.fusesource.wikitext', name: 'mediawiki-core', version: '1.4'
    compile group: 'org.fusesource.wikitext', name: 'confluence-core', version: '1.4'

    compile 'org.eclipse.jgit:org.eclipse.jgit:4.11.0.201803080745-r'
    compile 'org.eclipse.jgit:org.eclipse.jgit.http.server:4.11.0.201803080745-r'

    compile 'org.bouncycastle:bcprov-jdk15on:1.59'
    compile 'org.bouncycastle:bcmail-jdk15on:1.59'
    compile 'org.bouncycastle:bcpkix-jdk15on:1.59'

    compile 'org.apache.sshd:sshd-core:1.7.0'
    compile 'org.apache.mina:mina-core:2.0.16'

    compile group: 'org.apache.ivy', name: 'ivy', version: '2.4.0'
    compile group: 'org.apache.commons', name: 'commons-compress', version: '1.16.1'
    compile group: 'org.apache.tika', name: 'tika-core', version: '1.17'
    compile group: 'commons-io', name: 'commons-io', version: '2.6'
    compile group: 'commons-codec', name: 'commons-codec', version: '1.11'

    compile group: 'com.force.api', name: 'force-partner-api', version: '42.0.0'
    compile group: 'com.github.dblock.waffle', name: 'waffle-jna', version: '1.8.1'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.2'
    compile group: 'com.rometools', name: 'rome', version: '1.9.0'
    compile group: 'com.toedter', name: 'jcalendar', version: '1.4'
    compile group: 'com.unboundid', name: 'unboundid-ldapsdk', version: '4.0.4'
    compile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.14'
    compile group: 'org.freemarker', name: 'freemarker', version: '2.3.23'
    compile group: 'org.jsoup', name: 'jsoup', version: '1.11.2'
    compile group: 'org.kohsuke', name: 'libpam4j', version: '1.8'
    compile group: 'args4j', name: 'args4j', version: '2.33'
    compile group: 'redis.clients', name: 'jedis', version: '2.9.0'
    compile group: 'ro.fortsoft.pf4j', name: 'pf4j', version: '0.9.0' // TODO update

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '2.28.0'
    testCompile group: 'org.seleniumhq.selenium', name: 'selenium-support', version: '2.28.0'
    testCompile group: 'org.seleniumhq.selenium', name: 'selenium-firefox-driver', version: '2.28.0'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.10.19'
}

dependencies {
    def tomcatVersion = '8.5.28'
    tomcat "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}",
            "org.apache.tomcat.embed:tomcat-embed-logging-juli:8.5.2",
            "org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}"
}

tomcat {
    httpProtocol = 'org.apache.coyote.http11.Http11Nio2Protocol'
    ajpProtocol  = 'org.apache.coyote.ajp.AjpNio2Protocol'
}

def distributionDir = 'src/main/distrib'

task copyDistribution(type: Copy) {
    from "$distributionDir/data"
    into 'data'
}

task copyDefaultsProperties(type: Copy) {
    from "$distributionDir/data/defaults.properties"
    into 'src/main/resources/'
}

task copyClientAppsJson(type: Copy) {
    from "$distributionDir/data/clientapps.json"
    into 'src/main/resources/'
}

task generatePropertiesKeys() {
    // TODO convert properties keys from $distributionDir/data/defaults.properties to class com.gitblit.Keys
}

task copyResourcesToClasses(type: Copy) {
    from('src/main/resources') {
        exclude 'thumbs.db'
    }
    into 'build/classes/java/main'
}

task copyWebappToClasses(type: Copy) {
    from('src/main/webapp') {
        exclude 'thumbs.db'
    }
    into 'build/classes/java/main'
}

task runGitBlit << {
    println 'run'
}

runGitBlit.dependsOn copyDistribution
runGitBlit.dependsOn copyDefaultsProperties
runGitBlit.dependsOn copyClientAppsJson

runGitBlit.dependsOn generatePropertiesKeys
runGitBlit.dependsOn compileJava

runGitBlit.dependsOn copyResourcesToClasses
runGitBlit.dependsOn copyWebappToClasses

compileJava.mustRunAfter generatePropertiesKeys

run.dependsOn runGitBlit

jar.enabled = true
war {
    webInf {
        from("$distributionDir/data") {
            include 'users.conf', 'projects.conf', 'defaults.properties', 'gitblit.properties',
                    'git/project.mkd',
                    'groovy/sendmail.groovy', 'groovy/sendmail-html.groovy',
                    'groovy/jenkins.groovy', 'groovy/protect-refs.groovy',
                    'groovy/blockpush.groovy', 'groovy/localclone.groovy',
                    'groovy/fogbugz.groovy', 'groovy/thebuggenie.groovy',
                    'groovy/fisheye.groovy', 'groovy/redmine-fetch.groovy',
                    'groovy/subgit.groovy',
                    'gitignore/*.gitignore'
        }
        into 'data'
    }

    classpath = classpath.filter { file ->
        (!file.name.startsWith('jetty-all-'))
    }
}
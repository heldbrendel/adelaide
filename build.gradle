buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-tomcat-plugin:2.4.2'
    }
}

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'application'

apply plugin: 'maven'

apply plugin: 'idea'
apply plugin: 'eclipse'

apply plugin: 'com.bmuschko.tomcat'

group = 'com.gitblit'
version = '2.0.0-SNAPSHOT'

description = """Gitblit"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "http://repo1.maven.org/maven2" }
    maven { url "http://repo.eclipse.org/content/groups/snapshots" }
    maven { url "http://repo.eclipse.org/content/groups/releases" }
    maven { url "http://gitblit.github.io/gitblit-maven" }
}

compileJava.options.encoding = 'UTF-8'

sourceSets {
    main {
        java { srcDirs = ['src/main/java', 'src/main/bugtraq', 'src/main/gen'] }
        resources { srcDirs = ['src/main/resources'] }
    }
    test {
        java { srcDirs = ['src/test/java', 'src/test/bugtraq'] }
        resources { srcDirs = ['src/test/resources'] }
    }
}

/*
jar {
    manifest {
        attributes 'Main-Class': 'com.gitblit.GitBlitServer'
    }
}
*/

mainClassName = "com.gitblit.GitBlitServer"

dependencies {
    compile group: 'com.google.inject', name: 'guice', version: '4.0'
    compile group: 'com.google.inject.extensions', name: 'guice-servlet', version: '4.0-gb2'
    compile group: 'com.google.guava', name: 'guava', version: '18.0'

    compile group: 'com.intellij', name: 'annotations', version: '12.0'

    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.12'

    compile group: 'com.sun.mail', name: 'javax.mail', version: '1.5.1'
    compile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'

    compile group: 'org.eclipse.jetty.aggregate', name: 'jetty-all', version: '9.2.13.v20150730'

    compile group: 'org.apache.wicket', name: 'wicket', version: '1.4.22'
    compile group: 'org.apache.wicket', name: 'wicket-auth-roles', version: '1.4.22'
    compile group: 'org.apache.wicket', name: 'wicket-extensions', version: '1.4.22'

    compile group: 'org.apache.lucene', name: 'lucene-core', version: '5.5.2'
    compile group: 'org.apache.lucene', name: 'lucene-analyzers-common', version: '5.5.2'
    compile group: 'org.apache.lucene', name: 'lucene-highlighter', version: '5.5.2'
    compile group: 'org.apache.lucene', name: 'lucene-memory', version: '5.5.2'
    compile group: 'org.apache.lucene', name: 'lucene-queryparser', version: '5.5.2'

    compile group: 'org.pegdown', name: 'pegdown', version: '1.5.0'

    compile group: 'org.fusesource.wikitext', name: 'wikitext-core', version: '1.4'
    compile group: 'org.fusesource.wikitext', name: 'twiki-core', version: '1.4'
    compile group: 'org.fusesource.wikitext', name: 'textile-core', version: '1.4'
    compile group: 'org.fusesource.wikitext', name: 'tracwiki-core', version: '1.4'
    compile group: 'org.fusesource.wikitext', name: 'mediawiki-core', version: '1.4'
    compile group: 'org.fusesource.wikitext', name: 'confluence-core', version: '1.4'

    compile group: 'org.eclipse.jgit', name: 'org.eclipse.jgit', version: '4.1.1.201511131810-r'
    compile group: 'org.eclipse.jgit', name: 'org.eclipse.jgit.http.server', version: '4.1.1.201511131810-r'

    compile group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.52'
    compile group: 'org.bouncycastle', name: 'bcmail-jdk15on', version: '1.52'
    compile group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.52'

    compile group: 'org.apache.sshd', name: 'sshd-core', version: '1.0.0'
    compile group: 'org.apache.mina', name: 'mina-core', version: '2.0.9'

    compile group: 'rome', name: 'rome', version: '0.9'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.3.1'
    compile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.4'
    compile group: 'com.unboundid', name: 'unboundid-ldapsdk', version: '2.3.8'
    compile group: 'org.apache.ivy', name: 'ivy', version: '2.2.0'
    compile group: 'com.toedter', name: 'jcalendar', version: '1.3.2'
    compile group: 'org.apache.commons', name: 'commons-compress', version: '1.4.1'
    compile group: 'commons-io', name: 'commons-io', version: '2.2'
    compile group: 'com.force.api', name: 'force-partner-api', version: '24.0.0'
    compile group: 'org.freemarker', name: 'freemarker', version: '2.3.22'
    compile group: 'com.github.dblock.waffle', name: 'waffle-jna', version: '1.7.3'
    compile group: 'org.kohsuke', name: 'libpam4j', version: '1.8'
    compile group: 'args4j', name: 'args4j', version: '2.0.29'
    compile group: 'commons-codec', name: 'commons-codec', version: '1.7'
    compile group: 'redis.clients', name: 'jedis', version: '2.6.2'
    compile group: 'ro.fortsoft.pf4j', name: 'pf4j', version: '0.9.0'
    compile group: 'org.apache.tika', name: 'tika-core', version: '1.5'
    compile group: 'org.jsoup', name: 'jsoup', version: '1.7.3'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '2.28.0'
    testCompile group: 'org.seleniumhq.selenium', name: 'selenium-support', version: '2.28.0'
    testCompile group: 'org.seleniumhq.selenium', name: 'selenium-firefox-driver', version: '2.28.0'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.10.19'
}

dependencies {
    def tomcatVersion = '8.5.28'
    tomcat "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}",
            "org.apache.tomcat.embed:tomcat-embed-logging-juli:8.5.2",
            "org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}"
}

tomcat {
    httpProtocol = 'org.apache.coyote.http11.Http11Nio2Protocol'
    ajpProtocol  = 'org.apache.coyote.ajp.AjpNio2Protocol'
}

def distributionDir = 'src/main/distrib'

task copyDistribution(type: Copy) {
    from "$distributionDir/data"
    into 'data'
}

task copyDefaultsProperties(type: Copy) {
    from "$distributionDir/data/defaults.properties"
    into 'src/main/resources/'
}

task copyClientAppsJson(type: Copy) {
    from "$distributionDir/data/clientapps.json"
    into 'src/main/resources/'
}

task generatePropertiesKeys() {
    // TODO convert properties keys from $distributionDir/data/defaults.properties to class com.gitblit.Keys
}

task copyResourcesToClasses(type: Copy) {
    from('src/main/resources') {
        exclude 'thumbs.db'
    }
    into 'build/classes/java/main'
}

task copyWebappToClasses(type: Copy) {
    from('src/main/webapp') {
        exclude 'thumbs.db'
    }
    into 'build/classes/java/main'
}

task runGitBlit << {
    println 'run'
}

runGitBlit.dependsOn copyDistribution
runGitBlit.dependsOn copyDefaultsProperties
runGitBlit.dependsOn copyClientAppsJson

runGitBlit.dependsOn generatePropertiesKeys
runGitBlit.dependsOn compileJava

runGitBlit.dependsOn copyResourcesToClasses
runGitBlit.dependsOn copyWebappToClasses

compileJava.mustRunAfter generatePropertiesKeys

run.dependsOn runGitBlit

jar.enabled = true
war {
    webInf {
        from("$distributionDir/data") {
            include 'users.conf', 'projects.conf', 'defaults.properties', 'gitblit.properties',
                    'git/project.mkd',
                    'groovy/sendmail.groovy', 'groovy/sendmail-html.groovy',
                    'groovy/jenkins.groovy', 'groovy/protect-refs.groovy',
                    'groovy/blockpush.groovy', 'groovy/localclone.groovy',
                    'groovy/fogbugz.groovy', 'groovy/thebuggenie.groovy',
                    'groovy/fisheye.groovy', 'groovy/redmine-fetch.groovy',
                    'groovy/subgit.groovy',
                    'gitignore/*.gitignore'
        }
        into 'data'
    }

    classpath = classpath.filter { file ->
        (!file.name.startsWith('jetty-all-'))
    }
}
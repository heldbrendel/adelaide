buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-tomcat-plugin:2.5'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.20.0'
    }
}

plugins {
    id 'net.ltgt.apt' version '0.16'
    id "org.sonarqube" version "2.6.2"
    id "com.github.ben-manes.versions" version "0.20.0"
}

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'application'
apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'eclipse'

apply plugin: 'com.bmuschko.tomcat'

group = 'com.gitblit'
version = '2.0.0-SNAPSHOT'

description = """Gitblit"""

sourceCompatibility = 10
targetCompatibility = 10

repositories {
    jcenter()
    mavenCentral()
    maven { url "http://repo1.maven.org/maven2" }
    maven { url "http://repo.eclipse.org/content/groups/snapshots" }
    maven { url "http://repo.eclipse.org/content/groups/releases" }
    maven { url "http://gitblit.github.io/gitblit-maven" }
}

compileJava.options.encoding = 'UTF-8'

sourceSets {
    main {
        java { srcDirs = ['src/main/java', 'src/main/bugtraq', 'src/main/gen'] }
        resources { srcDirs = ['src/main/resources'] }
    }
    test {
        java { srcDirs = ['src/test/java', 'src/test/bugtraq'] }
        resources { srcDirs = ['src/test/resources'] }
    }
}

/*
jar {
    manifest {
        attributes 'Main-Class': 'com.gitblit.GitBlitServer'
    }
}
*/

mainClassName = "com.gitblit.GitBlitServer"

dependencies {
    compile 'com.google.inject:guice:4.2.0'
    compile 'com.google.inject.extensions:guice-servlet:4.2.0'
    compile 'com.google.guava:guava:25.1-jre'
    compile 'com.google.code.gson:gson:2.8.5'

    compile 'org.jetbrains:annotations:16.0.2'

    compile 'ch.qos.logback:logback-classic:1.2.3'
    compile 'org.slf4j:slf4j-api:1.7.12'

    compile 'com.sun.mail:javax.mail:1.6.1'
    compile 'javax.servlet:javax.servlet-api:4.0.1'

    compile 'org.eclipse.jetty.aggregate:jetty-all:9.2.13.v20150730' // TODO update?

    def wicketVersion = '8.0.0'
    compile "org.apache.wicket:wicket:$wicketVersion"
    compile "org.apache.wicket:wicket-auth-roles:$wicketVersion"
    compile "org.apache.wicket:wicket-extensions:$wicketVersion"

    def luceneVersion = '7.3.1'
    compile "org.apache.lucene:lucene-core:$luceneVersion"
    compile "org.apache.lucene:lucene-analyzers-common:$luceneVersion"
    compile "org.apache.lucene:lucene-highlighter:$luceneVersion"
    compile "org.apache.lucene:lucene-memory:$luceneVersion"
    compile "org.apache.lucene:lucene-queryparser:$luceneVersion"

    compile 'org.pegdown:pegdown:1.6.0'

    def wikiTextVersion = '1.4'
    compile "org.fusesource.wikitext:wikitext-core:$wikiTextVersion"
    compile "org.fusesource.wikitext:twiki-core:$wikiTextVersion"
    compile "org.fusesource.wikitext:textile-core:$wikiTextVersion"
    compile "org.fusesource.wikitext:tracwiki-core:$wikiTextVersion"
    compile "org.fusesource.wikitext:mediawiki-core:$wikiTextVersion"
    compile "org.fusesource.wikitext:confluence-core:$wikiTextVersion"

    compile 'org.eclipse.jgit:org.eclipse.jgit:4.11.0.201803080745-r'
    compile 'org.eclipse.jgit:org.eclipse.jgit.http.server:4.11.0.201803080745-r'

    compile 'org.bouncycastle:bcprov-jdk15on:1.59'
    compile 'org.bouncycastle:bcmail-jdk15on:1.59'
    compile 'org.bouncycastle:bcpkix-jdk15on:1.59'

    compile 'org.apache.sshd:sshd-core:1.7.0'
    compile 'org.apache.mina:mina-core:2.0.16'

    compile 'org.apache.ivy:ivy:2.4.0'
    compile 'org.apache.commons:commons-compress:1.17'
    compile 'org.apache.tika:tika-core:1.18'
    compile 'commons-io:commons-io:2.6'
    compile 'commons-codec:commons-codec:1.11'

    compile group: 'com.force.api', name: 'force-partner-api', version: '43.0.0'
    compile group: 'com.github.dblock.waffle', name: 'waffle-jna', version: '1.8.1'
    compile group: 'com.rometools', name: 'rome', version: '1.10.0'
    compile group: 'com.toedter', name: 'jcalendar', version: '1.4'
    compile group: 'com.unboundid', name: 'unboundid-ldapsdk', version: '4.0.6'
    compile 'org.codehaus.groovy:groovy-all:2.5.0'
    compile group: 'org.freemarker', name: 'freemarker', version: '2.3.28'
    compile group: 'org.jsoup', name: 'jsoup', version: '1.11.3'
    compile group: 'org.kohsuke', name: 'libpam4j', version: '1.10'
    compile group: 'args4j', name: 'args4j', version: '2.33'
    compile group: 'redis.clients', name: 'jedis', version: '2.9.0'
    compile group: 'ro.fortsoft.pf4j', name: 'pf4j', version: '0.9.0' // TODO update

    compileOnly 'org.projectlombok:lombok:1.18.0'
    annotationProcessor "org.projectlombok:lombok:1.18.0"

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '2.28.0'
    testCompile group: 'org.seleniumhq.selenium', name: 'selenium-support', version: '2.28.0'
    testCompile group: 'org.seleniumhq.selenium', name: 'selenium-firefox-driver', version: '2.28.0'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.10.19'


    def tomcatVersion = '8.5.32'
    tomcat "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}",
            "org.apache.tomcat.embed:tomcat-embed-logging-juli:8.5.2",
            "org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}"
}

tomcat {
    httpProtocol = 'org.apache.coyote.http11.Http11Nio2Protocol'
    ajpProtocol  = 'org.apache.coyote.ajp.AjpNio2Protocol'
}

def distributionDir = 'src/main/distrib'

task generatePropertiesKeys() {
    // TODO convert properties keys from $distributionDir/data/defaults.properties to class com.gitblit.Keys
}

compileJava.mustRunAfter generatePropertiesKeys

jar.enabled = true
war {
    archiveName 'gitblit.war'

    classpath = classpath.filter { file ->
        (!file.name.startsWith('jetty-all-'))
    }
}